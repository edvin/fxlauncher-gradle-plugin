/*
 * Copyright 2016 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// http://central.sonatype.org/pages/gradle.html

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
    }
}

plugins {
    id 'maven'
    id 'signing'
    id 'groovy'
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'junit:junit:4.12'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

apply plugin: "com.gradle.plugin-publish"

tasks.withType(JavaCompile) {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

signing {
    sign configurations.archives
}

group = "no.tornado"
archivesBaseName = "fxlauncher-gradle-plugin"
version = "1.0.21.1"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'FXLauncher Gradle plugin'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'FXLauncher Gradle plugin'
                url 'https://github.com/edvin/fxlauncher-gradle-plugin'

                scm {
                    connection 'scm:svn:https://github.com/edvin/fxlauncher-gradle-plugin'
                    developerConnection 'scm:svn:https://github.com/edvin/fxlauncher-gradle-plugin'
                    url 'https://github.com/edvin/fxlauncher-gradle-plugin'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'aalmiray'
                        name 'Andres Almiray'
                    }
                    developer {
                        id 'edvin'
                        name 'Edvin Syse'
                        email 'es@syse.no'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

pluginBundle {
    website = 'https://github.com/edvin/fxlauncher-gradle-plugin'
    vcsUrl = 'https://github.com/edvin/fxlauncher-gradle-plugin'
    description = 'FXLauncher plugin'
    tags = ['fxlauncher', 'javafx']

    plugins {
        fxlauncherPlugin {
            id = 'no.tornado.fxlauncher'
            displayName = 'FXLauncher Gradle plugin'
        }
    }
}